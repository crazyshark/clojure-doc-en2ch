#Start at Fri 14, Oct 2011
#First version completed at Sun 30, Oct 2011, no collation.



# 什么是序列？ #

在Clojure中，任何逻辑上可看做数据项集合（collection of items）的数据结构都可以使用序列这样一种统一的方式来读取，写入和修改。在非常基础的层次上，序列被整合进了Clojure，同时也是目前为止最为方便和符合规范的处理集合的方式。他们的角色等同于列表（lists）在其他Lisp方言中的角色。但是，序列不仅仅是一个操作集合的API(a collection API)，他们通常还作为框架（framework）以方便Clojure程序中流程和逻辑的构建，同时，简单易用的设计也使他们作为递归和高阶函数相关应用的基础。

从根本上来说，序列是一种抽象，将对所有集合的一些通用行为泛化为一些通用的编程接口，然后将其通过一个由序列函数（sequence functions）组成的库（library）暴露出来以方便使用。基于对链接表（linked list）的一些经典的操作，例如“`first`”和“`rest`”，（或者“`car`”和“`cdr`”，对那些有lisp背景的人来说）的观察，我们设计出了序列，而且可以说序列在任何数据类型上都能工作的同样好。举个例子，`first`函数返回一个序列的第一个项（item）。无论这个序列实际上是一个list，vector（向量），set，或者甚至是一个map（键值对），都没有任何区别。

```clj

user=> (def mylist '(1 2 3))
user=> (first mylist)
1

user=> (def myvec [1 2 3])
user=> (first myvec)
1

user=> (def myset #{1 2 3})
user=> (first myset)
1

user=> (def mymap {:a 1 :b 2 :c 3})
user=> (first mymap)
[:a 1}
```

相似的，`rest`函数对于任何序列返回剔除了第一个项的一个新的序列：

```clj

user=> (def mylist '(1 2 3))
user=> (rest mylist)
(2 3)
```

序列函数是非常有用的。举个例子，只需要`first`和`rest`（还需要另一个函数，`empty?`，如果一个序列为空则放回true）就能实现一个Lisp中极为常见的惯用语：递归遍历一个list的函数。但是，因为你使用的是序列，除了list，你可以遍历任何数据集合。

```clj

(defn printall [s]
(if (not (empty? s))
(do
(println (str "Item: " (first s)))
(recur (rest s)))))
```

这个函数以一个序列作为输入参数，并建厂这个序列是否为空。如果为空，这个函数什么都不做（隐式返回nil）。如果序列中含有项，它就以副作用（side effect）的形式将字符串“Item:”与序列中第一项连接并打印出来。然后函数递归，将序列中剩下的项传入下一个迭代中。函数可以以list为输入参数：

```clj

user=> (printall '(1 2 3))
Item: 1
Item: 2
Item: 3
nil
```

或者是vector：
```clj

user=> (printall ["vector" "of" "strings"])
Item: vector
Item: of
Item: strings
nil
```

甚至是字符串也行得通，因为字符串可以看做是字符的序列：
```clj

user=> (printall " Hello")
Item: H
Item: e
Item: l
Item: l
Item: o
nil
```

正是因为序列是如此的泛化，同一个函数可以在所有这些完全不同的集合类型上表现如此完美。


---


**注意** 从技术的角度讲，这些不同类型的数据结构本身并不是序列，而是可以通过使用`seq`转化为序列。函数`seq`以蛋哥参数作为输入，为其构造一个序列视图。比如，向量并非序列，但是`(seq any-vector)`的结果却是一个序列。因为几乎所有的序列函数都在内部隐含的调用了`seq`，所以在大多数实际应用的时候，是否使用一个序列作为参数并没有太大的差别。但是，有一个地方需要注意，如果你碰到一个需要序列，而不是一个可序列化（sequence-able）的集合作为参数的函数：这里就有一些差别了。你可以使用`seq`在任何collection之上高效地获取一个序列视图。


---


## 可序列化的类型 ##

序列几乎可以从任何语言支持的collection类型上创建。

  * Clojure的持久化集合：Maps，sets， lists， 以及vectors都能作为序列很好地工作。
  * 字符串：所有字符串都是字符的序列。
  * Java数组：但是这可能会造成匹配不当，因为Java数组是可变的，而序列不会试着去避免复杂的故障（difficult bugs）。当你使用一个构建在数组之上的序列的时候，试着避免去修改这个数组
  * 任何实现了java.lang.Iterable接口的Java collection。同样的，相对于序列的不可变，Java collection是可变的，所以在使用的时候应当避免去修改collections。
  * 本地支持：序列也可以不需要任何其他collection类型的支持而直接构造。

# 剖析序列 #

理解序列的底层逻辑结构是非常重要的。以不同方式构造的序列在实现上有很大程度的差异。一个vector的序列表示，起本质上依旧是一个vector，拥有和vector一样的性能特性（performance characteristics）。但是所有的序列共享一个相同的概念模型：根据`first`和`rest`实现的一个单向链接（singly-linked）list。顺便说一句，`first`和`rest`和传统Lisp中的`car`和`cdr`是完全相同的。他们以便现代程序员更为熟悉的属于被重命名，同时也更为准确的表现了他们的意图。

每个序列，从概念上讲，都是有两个部分组成：使用`first`函数访问的序列中的第一个项，即`first`部分，以及使用`rest`函数访问的序列中所有其他剩下的项，即`rest`部分。每个包含n项的序列序列实际上都有n-1个组件序列（component sequences）组合而成。序列在`rest`返回为空的时候终结。所有其他的序列函数都可以根据`first`和`rest`来定义，虽然从collection类型创建的序列为了获得更好的性能直接实现了他们（指其他的序列函数）。

图5-1 序列示例，展示组件序列

# 构建序列 #

使用这样的序列模型，使用`cons`或者`conj`两个函数来直接构建序列都是相当容易的。`cons`函数意为“构件（construct）”，它需要两个参数作为输入，一个项和一个序列。它使用这个项作为第一项（first），这个序列作为所有剩下的项（rest）构件一个新的序列。一个由`cons`构建的序列被称为“构建单元（cons cell）”——一个简单的first/rest对。任何长度的序列都可以通过将多个构件单元链接的方式来构建。

```clj

user=> (cons 4 '(1 2 3))
(4 1 2 3)
```

`conj`函数与`cons`函数相似，表示“联合（conjoin）”。它与`cons`的最主要的区别是（如果可能的话）它会重用序列的底层实现而不是总去构建“构建单元”。这样做通常会使得到的序列工作更为高效。新的项是添加到序列的最前端还是最后段是更具序列的底层实现来决定的。不同于`cons`，`conj`使用一个序列作为它的第一个输入参数，将要添加的项反而作为第二个参数：

```clj

user=> (conj '(1 2 3) 4)
(4 1 2 3)
```

`conj`也支持一次性添加多个项：只需要是用多个附加参数即可。序列按他们提供的顺序被添加到序列的前端：

```clj

user=> (conj '(1 2 3) 4 5 6)
(6 5 4 1 2 3)
```


---


**注意** `conj`的一个特性需要留意一下，它在内部并不会在它的输入参数上调用`seq`。它也可以直接工作在数据结构（data structures）上，与工作在序列之上没有任何差别。在这种情况下，它会根据特定数据结构将新的项以最高效的方式添加，而不是必须将其添加到最前方（它对序列是这样做的）。就拿vector来说，添加项最为高效的地方是vector的最末端。所以`(conj [1 2] 3)`将会得到`[3 1 2]`。如果你知道你需要一个序列，而且你也希望将项添加到最前方，在vector上调用`seq`：`(conj (seq [1 2]) 3)`将得到你想要的`(3 1 2)`。你也可以使用`cons`来代替。在不想将你的collection类型转化为序列的时候去使用`conj`。


---


对于`conj`和`cons`，如果你在需要序列作为输入的地方填入了nil，它会构建一个仅包含你提供的那个项的序列。

```clj

user=> (cons 1 nil)
(1)
```

接下来是另一个很常见的Lisp惯用法，使用`cons`或者`conj`来递归地构建一个list。下面的这个函数展示了使用从1到用户输入的参数之间的所有整数来递归构造的一个序列：

```clj

(defn make-int-seq [max]
(loop [acc nil n max]
(if (zero? n)
acc
(recur (cons n acc) (dec n)))))
```

在每次迭代中，这个函数将n（初始为最大值）用`cons`函数添加到一个累加器（accumulator）序列（初始化为nil）中，然后递归，将新的累加器和新的被减一（decremented）的n传入。当n累减直0之后，函数将这个累加器序列返回，这时它已经包含了所有1到最大值。

```clj

user=> (make-int-seq 5)
(1 2 3 4 5)
```

# 延迟加载的序列 #

延迟加载（laziness，等同于惰性），Clojure中序列另一个极为重要的方面，是以first/rest结构为基础的。延迟加载为操作那些数据量太大以至于无法一次加载到系统内存的数据提供了一种概念上十分简单，同时也相当搞笑的方式。这些数据可以具有无穷的长度，却依旧可以被任意标准序列函数高效地使用。延迟加载的序列作为一种高阶抽象，它是开发者能够专注于那些正在进行的计算，而不是去管理加载和创造数据的繁琐细节。

延迟加载这种概念是根据这样一个观察和研究得到的：逻辑上讲，一个序列的`rest`部分实际上并不是必须存在的，只要可以在需要的时候创建就行了。延迟加载序列的`rest`部分可以被实现为返回一个序列的函数，而不是包含一个实际的明确的一系列值。从使用这个序列的函数来看，这并没有什么差别；当他们调用`rest`函数的时候，他们得到了一个序列。不同仅仅在于如果是一个一般的序列，它会返回一个已经存在与内存中的数据结构。在延迟加载序列中，调用`rest`实际上计算并初始化了一个新的序列，这个序列使用刚刚计算出来的值作为它的`first`，使用一系列有关如何生成更多的值的指令（一系列代码）作为它的`rest`。

考虑到效率问题，一旦延迟加载的序列被实现（realize），得到的值将被作为正常的非延迟加载的序列缓存下来——之后对这个序列的访问将会以正常的方式访问，而不是被延迟地产生。这样保证了延迟加载所需的计算只会被运行一次：在延迟加载的时候使用大量的，沉重繁复的的计算将不会导致任何的性能问题，因为这种机制保证了它们只会运行一次。只要有代码在使用这些缓存的值，它们就会一直存在。当没有任何引用存在的时，这些缓存的值会像其他所有对象一样被作为垃圾回收。

## 一个延迟加载的例子 ##

想要看看延迟加载序列如何工作，可以想想`map`函数。`map`函数是Clojure中一个极为重要的序列操作工具。它需要一个序列和一个函数作为输入参数，它将这个函数应用到所提供序列中每一项，根据返回值得到一个新的序列。举个例子，如果你将序列`'(1 2 3 4 5 6 7)`和一个将参数平方的函数，`(fn [x] (*x x))`传入`map`，它的返回值则为`(1 4 9 16 25 36 49)`。这就是将所有原序列中的项平方之后得到的结果。

```clj

user => (map
(fn [x] (* x x))
'(1 2 3 4 5 6 7))
(1 4 9 16 25 36 49)
```

`map`的返回值实际上是一个延迟加载的序列，看到这一点并不是那么容易。因为返回值是立即就输出到了REPL中，差别几乎是看不到的——实际的值立刻就被实现了。

为了看到延迟加载序列的内部工作情况，让我们向你的平方方程添加一点附带的功能（side effect），这样你就能看到运算是什么时候发生的（一般来说，在传递给`map`的函数中添加副作用不是一个好的设计实践，但是在这里这样做能展示延迟加载的一些细节）。在新的平方函数中，你会在每个参数的值被处理的时候将其打印出来。让我们把事情简化一点，用`defn`而不是以内联的形式将这个新函数传递给`map`：

```clj

(defn square [x]
(do
(println (str "Processing: " x))
(* x x)))
```

除了用`do`来实现一点副作用，将参数的值在被处理之前给打印出来，这个函数跟它的上一个版本完全是一样的。它返回一个比较出人意料而且有点混乱的结果：

```clj

user => (map square '(1 2 3 4 5)
(Processing:1
Processing:2
1 Processing:3
4 Processing:4
9 Processing:5
16 25)
```

结果如此难看的原因是`println`在打印输出结果的过程中被调用。包含`println`调用的平方函数知道它真正被需要的时候才会被调用——知道系统真的需要实现这个延迟加载的值得时候。所以你的这些`println`调试语句和函数的实际输出：“（1 4 9 16 25）”完全搀和在了一起。

为了让事情更为清除，让我们对`map`的调用结果赋值给一个符号（symbol）：

```clj

user =>(def map-result (map square '(1 2 3 4 5))
#'user/map-result
```

你现在有了一个符号，按理说，应该是绑定到一个由平方值组成的序列。但是，你没有看到你的调试语句。`square`完全没有被调用！`map-result`是一个延迟加载的序列。所以，逻辑上讲它包含了你想要的那些平方值，但是他们还没有被实现。所以它并不是一个平方值的序列，而是一个平方值序列的承诺（promise）。你可以把它传递到程序的任何地方，或者把它存储起来，实际的运算工作会等到真正需要的时候再进行。

现在，让我们使用`nth`函数来获取序列的几个值。`nth`函数可以根据输入参数获取序列某个位置的值。调用`(nth map-result 2)`应该返回9，因为3的平方值是9，同时3是原序列中第二个(2nd)个项（从0开始，因为Clojure中所有的索引都是从0开始的）。

```clj

user => (nth map-result 2)
Processing:1
Processing:2
Processing:3
9
```

你可以从调试语句中发现函数`square`被调用了三次——刚好足够来计算序列中第三个值。但是，再次运行相同的函数调用不会再触发`square`函数了：

```clj

user => (nth map-result 2)
9
```

实现的值已经被缓存了，所以没有必要再次调用`square`去计算他们。现在，打印出序列的所有值：

```clj

user => (println map-result)
(1 4 Processing:4
9 Processing:5
16 25)
```

只调用了`square`两次，只是为了计算序列中两个剩下的还未实现的值。缓存的值并未重新计算。

这个例子展示了`map`函数返回的延迟加载序列是如何将实际运算推迟到这些值真正需要的时候再进行。

## 构建延迟加载的序列 ##

获取一个延迟加载序列是相当简单的。大多数Clojure的内置序列函数，诸如`map`和`filter`都默认返回延迟加载的序列。如果你想要生成你自己的延迟加载序列，有两种方法可以实现：直接构造一个，或者使用一个生成延迟加载序列的函数来帮你。

### 直接构造延迟加载的序列 ###

想要手动构建延迟加载的序列，可以使用内建的`lazy-seq`宏，将原本会返回一般的非延迟加载序列的代码给包装起来。`lazy-seq`将任何它包装的代码所返回的值延迟化，从而构建一个延迟加载的序列。这些代码不会理解执行，而是被放到延迟加载序列的上下文中留到以后执行（saved for later）。

举个例子，下面这个函数通过设置一个基准值，然后不断地把另一个数值加给它，以构造一个延迟加载序列。

```clj

(defn lazy-counter [base increment]
(lazy-seq
(cons base (lazy-counter (+ base increment) increment))))
```

然后，你就可以调用这个函数，并使用`take`函数来获取延迟加载序列的开始几个值。（`take`有两个输入参数，一个整数和一个集合。它从这个集合中获取指定的项，返回由这些项组成的序列）

```clj

user=> (take 10 (lazy-counter 0 2))
(0 2 4 6 8 10 12 14 16 18)
```

这个序列从逻辑上说，真的是无限长的。打个比方，从2开始，每次加3，使用`nth`来获取第一百万个项：

```clj

user=> (nth (lazy-counter 2 3) 1000000)
3000002
```

因为是无限长的，你可以用`lazy-counter`来获取一个任意长度的序列——唯一的限制会变成你的电脑会花多少时间去将一百万个、十亿个或者是任何你选择的数量的数字加在一起。

可以把它跟非延迟加载的版本比较一下：

```clj

(defn counter [base increment]
(cons base (counter (+ base increment) increment)))
```

这个函数甚至还没开始真正运行就失败了。它几乎立即就在`StackOverflowError`面前倒下了。因为它没有推迟任何运算，它总是立即选择递归直至将JVM中的栈空间耗尽。延迟加载的版本不会有这个问题。虽然它也是以递归的方式定义的，但是，只有当处理延迟加载学列的内部代码准备好展开下一个值得时候，`lazy-seq`中包含的内容才会被执行。这样做不会消耗栈空间，所以延迟加载序列可以做到即十分高效，又能在逻辑上达到长度无限。


---


**注意** 在处理无限长序列的时候需要特别注意。他们在逻辑上是无限长的，但是要注意别去尝试实现（展开）无穷多的值。比如，试着去在REPL中直接答应一个无限长的延迟加载序列，如果你没有使用`take`或相类似的函数，程序会在延迟加载序列上大量产生数值，直到无穷永不停止，最后被锁住。在这种或是其他相类似的情景中，写出不停去处理一个无限长序列，并最终将所在的线程完全锁住的代码是完全有可能的。无限长的序列能够十分有用，但是一定要确定在使用它们的时候设定了合适的结束条件，而不是依赖于达到序列的结尾处。仅仅因为序列是无限长的不代表你要花费无穷多的时间去处理它，或者试着一次将所有的东西都加在到内存中。很可惜，电脑是能力有限的机器，经不起这样折腾。


---


### 使用序列产生函数来构造延迟加载序列 ###

许多需要延迟加载序列的常见情况中，使用序列产生函数（sequence generator function）通常会比直接使用`lazy-seq`更为简便。`iterate`函数就是特别有用的。它通过将序列中之前的一个项作为输入参数调用提供的函数，把得到的值作为下一个项，以产生一个无限长的序列。它需要两个输入参数：用来调用的函数和序列中第一个项。

比如，把`iterate`和内建的递增函数`inc`联合起来生成一个所有整数组成的无限长延迟加载序列：

```clj

user=> (def integers (iterate inc 0))
#'user/integers
user=> (take 10 integers)
(0 1 2 3 4 5 6 7 8 9)
```

通过提供一个特定的函数，`iterate`可以用来提供与之前提到的`lazy-counter`函数完全相同的功能：

```clj

(defn lazy-counter-iterate [base increment]
(iterate (fn [n] (+ n increment)) base))
user=> (nth (lazy-counter-iterate 2 3) 1000000)
3000002
```

还有几个与`iterate`功能相似的函数：请查看“序列API”章节。

### 延迟加载序列和内存管理 ###

理解延迟加载序列是如何占用内存是很重要的。以一种高效利用内存的方式使用巨大的甚至是无限长的序列是可能的，但不幸的是也可能一不小心就消耗了大量的内存，甚至在超过能够使用的JVM实例提供的堆空间最大值，直接导致`OutOfMemoryError`。

使用以下几个准则来推敲延迟加载序列是怎样占用内存的：
  * 还未实现的延迟加载序列只占用几乎可以忽略的内存（除了用来保存它们定义的几个字节）。
  * 一旦一个延迟加载序列被实现之后，它包含的所有值都会占用内存。假使有一个引用指向这个实现后的序列，知道这个引用被丢弃之后序列才会被回收。

还有最后一点区别，但却是关键。下面两段代码展示了这种差异：

```clj

user=> (def integers (iterate inc 0))
#'user/integers
user=> (nth integers 1000000)
1000000
```

第二段：

```clj

user=> (nth (iterate inc 0) 1000000)
1000000
```

尽管这两段代码几乎做的是完全相同的事情，剖析（profiling）JVM表明前一段代码中，大概60MB的堆空间在`nth`调用后被消耗，但是后一段代码几乎没有造成内存的消耗。为什么？

在第一个例子中，延迟加载序列被一个符号引用了。序列最开始没有被实现，只占用了非常少量的内存。但是，为了获取想要的值，`nth`必须将序列的值逐个实现。现在，`integers`这个符号所引用的序列缓存了从0到1000000的所有值，是这些值占用了内存空间。

那么为什么第二个例子没有使用如此多的内存空间呢？答案是`nth`函数本身并不会保存任何引用。当它顺着一个序列进行取值的时候，它从每个新的序列那里获取`rest`，然后将所有对这个序列本身的引用丢弃。被`(iterate inc 0)`构造的序列作为初始化的参数，但是不同于第一个例子，这里并没有维护这个序列的的永久引用，`nth`几乎是在处理的过程中立即就“忘记”了它。没有任何值被缓存，所以也没有内存被使用。

所有的内建序列函数，比如`nth`，都很小心的没有去维护任何消耗内存的引用，所以保证合理的内存使用是开发者的责任。跟踪内存的使用情况主要在于追踪延迟加载序列的引用。

可能一开始这听起来有点复杂，但当你习惯了Clojure的工作方式，排除那些不相干的引用只是小菜一碟。Clojure自身对纯净函数的重视，对于减少（discourage）那些不加选择地构造引用的行为有极大的帮助。编写你自己的处理序列（sequence-consuming）函数应该是你唯一可能，也很容易犯错误的地方，但是只要你保持清醒的头脑，知道哪些符号引用了潜在的无限长序列，不犯错误应该不是什么难事。重要的是你要知道当遇到`OutOfMemoryErro`的时候应该去关注哪些环节。

# 序列API #

Clojure提供了一整套操纵序列的函数。熟悉他们，了解他们的能力将能省掉你大量的精力，因为你经常会发现可以将一大段数量惊人的代码替换为对其中某个函数的调用。

## 构造序列 ##

这一节的函数提供了多种不同的方法来构建序列，包括直接构建或者是基于已有的数据结构之上。

  * seq
> > `seq`函数需要一个collection作为输入参数，返回这个collection的一个序列表象（a sequence representaion）。大多数序列操纵函数都隐式地在他们的输入参数上调用`seq`，所以他们可以接受各种collection，而不用手动地去调用`seq`。

```clj

user=> (seq [1 2 3 4 5])
(1 2 3 4 5)
user=> (seq {:a 1 :b 2 :c 3})
([:a 1] [:b 2] [:c 3])
```

  * vals
> > `vals`以一个map作为输入参数，返回一个由map所有的值（value）组成的序列。

```clj

user=> (vals {:key1 "value1" :key2 "value2" :key3 "value3"})
(" value1" "value2" "value3")
```

  * keys
> > `keys`以一个map作为输入参数，返回一个由map所有的键（key）组成的序列。

```clj

user=> (keys {:key1 "value1" :key2 "value2" :key3 "value3"})
(:key1 :key2 :key3)
```

  * rseq
> > `rseq`需要一个输入参数，这个参数只能是一个vector或者是一个sorted map。它返回一个由他们的值逆向排列组成的序列，这个操作执行的时间是常量值。

```clj

user=> (rseq [1 2 3 4])
(4 3 2 1)
```

  * lazy-seq
> > `lazy-seq`作为一个宏，用来包裹一个返回序列的form。它产生一个延迟加载的序列，在前一节“直接构建延迟加载的序列”中已经详细讨论过了。

  * repeatedly
> > `repeatedly`需要一个输入参数，一个不能有输入参数的函数，返回由不断调用这个函数得到的返回值组成的延迟加载的序列。需要注意一点，如果这个函数是一个纯函数（pure function），它每次都会返回相同的值，因为它没有输入参数。

```clj

user=> (take 5 (repeatedly (fn [] "hello")))
("hello" "hello" "hello" "hello" "hello")
```

通常，`repeatedly`与一个不纯的函数（impure function）一块儿工作会更有用，比如一个机遇`rand-int`的函数，它会返回0和它输入参数之间的一个任意数字。

```clj

user=> (take 5 (repeatedly (fn [] (rand-int 5))))
(3 0 4 3 2)
```

  * iterate
> > `iterate`需要两个输入参数：一个单输入参数的函数和一个值。它以这个值为初始值，把所提供函数以这个值为参数调用，得到的值作为第二项并作为参数再次调用函数，以此类推得到的一系列值作为延迟加载序列返回。

```clj

user=> (take 10 (iterate inc 5))
(5 6 7 8 9 10 11 12 13 14)
```

这个例子使用递增函数`inc`来产生一个从5开始的无限长整数序列。查看前一节“使用序列申城函数构建延迟加载的序列”中的讨论来了解更多细节。

  * repeat
> > `repeat`需要一个或者两个输入参数。一个参数的版本返回以这个参数的值无尽重复组成的无限长延迟加载序列。

```clj

user=> (take 5 (repeat "hi"))
("hi" "hi" "hi" "hi" "hi")
```


> 两个参数的版本以一个数字作为第一个参数，一个值为第二个参数，它返回以第二个参数无尽重复的序列，第一个参数指定了这个序列的长度。

```clj

user=> (repeat 5 "hi")
("hi" "hi" "hi" "hi" "hi")
```

  * range
> > `rang`的参数个数可以为一个、两个或者三个。一个参数的版本以一个数字作为参数，返回一个从0（包含）到这个数字（不包含）间所有数字组成的延迟加载的序列。

```clj

user=> (range 5)
(0 1 2 3 4)
```


> 两个参数的版本以两个数字作为参数，返回一个从第一个数字（包含）到第二个数字（不包含）之间所有数字组成的延迟加载序列。

```clj

user=> (range 5 10)
(5 6 7 8 9)
```

> 三个参数的版本需要三个数字作为参数，还是返回一个延迟加载的序列，以第一个数字（包含）为起点，第二个数字（不包含）为终点，不同的是以第三个数字作为递增值。

```clj

user=> (range 4 16 2)
(4 6 8 10 12 14)
```

  * distinct
> > `distinct`以一个序列或collection作为参数。返回一个序列，剔除了所有的重复值。

```clj

user=> (distinct [1 2 2 3 3 4 1])
(1 2 3 4)
```

  * filter
> > `filter`需要两个参数：一个单输入参数返回布尔值的断言函数（predicate function，谓词），和一个序列/collection。返回一个延迟加载序列，它只包含这个序列/collection中输入到断言函数中返回值为true的那些值。

```clj

user=> (filter (fn [s] (= \a (first s))) ["ant" "bee" "ape" "cat" "dog"])
("ant" "ape")
```


> 在这个例子中，断言函数检测它的参数是否以一个“a”字符作为起始字符。

  * remove
> > `remove`和filter类似，但是它将断言函数返回值为true的那些值剔除了。

```clj

user=> (remove (fn [s] (= \a (first s))) ["ant" "bee" "ape" "cat" "dog"])
("bee" "cat" "dog")
```

  * cons
> > `cons`需要两个参数，一个值和一个序列/collection。它将这个值附加到这个序列/collection中，并返回一个序列（先调用seq函数得到一个序列，然后将值附加在序列的最前方）。

```clj

user=> (cons 1 [ 2 3 4])
(1 2 3 4)
```

  * concat
> > `concat`可以接受任意数量的输入参数，只要是序列/collection就可以。它把这些序列串联起来，返回一个他们组成的延迟加载序列。

```clj

user=> (concat [1 2 3] '(4 5 6) [7 8 9])
(1 2 3 4 5 6 7 8 9)
```

  * lazy-cat
> > `lazy-cat`序列也是接受任意数量的序列/collection作为输入参数（也可以是任意form，只要能求值得到序列或collection）。调用它得到一个连接所有这些序列的延迟加载序列。`lazy-cat`与`concat`不同，那些作为输入参数的form直到需要的时候才会被求值（evaluate）。就像`lazy-cat`的字面意思一样，它和`concat`很像，但是延迟性更好（lazier）。当结果可能不会被完全使用的时候使用`lazy-cat`,这样对所提供的一些form的求值工作就能省去了。

```clj

user=> (lazy-cat [1 2 3] ‘(4 5 6) [7 8 9])
(1 2 3 4 5 6 7 8 9)
```

  * mapcat
> > `mapcat`以一个函数作为第一个参数，然后任意数量的序列/collection作为附加参数。它把这个函数应用到所有这些序列或collection上，然后把所得到的结果连接起来。`mapcat`在使用`concat`连接这些结果的时候，会假设这个函数会返回一个collection或序列。

```clj

user=> (mapcat (fn [x] (repeat 3 x)) [1 2 3])
(1 1 1 2 2 2 3 3 3)
```


> 这个例子中，提供的函数返回一个由三个输入参数组成的序列。`mapcat`将函数应用到剩下这些序列或collection之后再将它们连接起来。

  * cycle
> > `cycle`需要一个序列/collection作为输入参数，返回一个延迟加载序列，由反复地重复这个序列或collection中的值组成。

```clj

user=> (take 10 (cycle [:a :b :c]))
(:a :b :c :a :b :c :a)
```

  * interleave
> > `interleave`以任意数量的序列/collection作为输入参数。它返回一个延迟加载序列，它是这样组成的：拿出每个参数序列的第一个项放到新序列中，然后是第二个项，第三个，以此类推。它会在任意一个序列达到尾部的时候停止。

```clj

user=> (interleave [:a :b :c] [1 2 3])
(:a 1 :b 2 :c 3)

user=> (interleave [:a :b :c] (iterate inc 1)]
(:a 1 :b 2 :c 3)

user=> (interleave [:a :b :c] [1 2 3] [\A \B \C])
(:a 1 \A :b 2 \B :c 3 \C)
```

  * interpose
> > `interpose`需要两个参数，一个值和一个序列/collection，它通过将这个值插入到序列或collection的值之间的方式得到一个延迟加载的序列。

```clj

user=> (interpose :a [1 2 3 4])
(1 :a 2 :a 3 :a 4 :a 5)
```

  * rest
> > `rest`以一个序列/collection作为参数。返回剔除了第一项的一个新序列（会从collection转换到序列）。如果没有更多的项（输入的序列/collection只有一项），它会返回一个空序列。

```clj

user=> (rest [1 2 3 4])
(2 3 4)

user=> (rest [])
()
```

  * next
> > `next`与`rest`几乎完全相同，除了一点，如果没有更多的项，它会返回`nil`。

```clj

user=> (next [1 2 3 4])
(2 3 4)
user=> (next [])
nil
```

  * drop
> > `drop`需要两个输入参数：一个数字和一个序列/collection。它返回原序列中指定数值之后的项组成的新序列。如果没有足够的项，它会返回一个空序列。

```clj

user=> (drop 2 [:a :b :c :d :e])
(:c :d :e)
```

  * drop-while
> > `drop-while`需要两个参数，一个单输入参数返回布尔值的断言函数和一个序列/collection。它返回一个新的序列，包含原序列中从第一个为之调用断言函数返回值为false的项起的所有项。

```clj

user=> (drop-while pos? [2 1 5 -3 6 -2 -1])
(-3 6 -2 -1)
```


> 这个例子使用了`pos?`断言函数，它为所有大于零的值返回true，反之为false；

  * take
> > `take`需要两个参数，一个数字和一个序列/collection。 它返回一个新的序列，由原序列中从最前方第一项开始指定数目的项组成。

```clj

user=> (take 2 [1 2 3 4 5])
(1 2)
```

  * take-nth
> > `take-nth`需要两个输入参数，一个数字和一个序列/collection。它返回一个新序列，由原序列中第一项和所有以N为步长在原序列中选择到的值（1+kN），N为第一个输入参数。

```clj

user=> (take-nth 3 [1 2 3 4 5 6 7 8 9 10])
(1 4 7 10)
```

  * take-while
> > `take-while`需要两个参数，一个单输入参数返回布尔值的断言函数和一个序列/collection。它返回原序列中从第一个项起始，直到第一个为之调用断言函数返回值喂false的项组成的新序列。

```clj

user=> (take-while pos? [2 1 5 -3 6 -2 -1])
(2 1 5)
```

  * drop-last
> > `drop-last`接受一个或两个参数。一个参数的版本接受一个序列/collection。返回剔除了最后一项的新序列。

```clj

user=> (drop-last [1 2 3 4 5])
(1 2 3 4)
```


> 两个参数的版本需要一个数字和一个序列/collection。返回剔除了最后N项的新序列，N为第一个参数。

```clj

user=> (drop-last 2 [1 2 3 4 5])
(1 2 3)
```

  * reverse
> > `reverse`需要一个参数，一个序列/collection。它将原序列逆序返回。`reverse`非延迟加载。

```clj

user=> (reverse [1 2 3 4 5])
(5 4 3 2 1)
```

  * sort
> > `sort`需要一个或两个输入参数，一个参数的版本需要一个序列/collection。返回基于序列中项自然排序后组成的新序列。

```clj

user=> (sort [2 3 5 4 1])
(1 2 3 4 5)
```


> 两个参数的版本接受一个实现了`java.util.Comparator`的对象和一个序列/collection。返回依据这个比较器进行排序后的结果。

  * sort-by
> > `sort-by`需要两个或三个参数。两个参数的版本需要一个单输入参数的键函数（key function）和一个序列/collection。返回基于为序列中每一项调用键函数返回值进行排序的新序列。键函数需要能够返回一个本身能进行排序的值，如一个字符串或是一个数字。

```clj

user=> (sort-by (fn [n] (/ 1 n)) [2 3 5 4 1])
(5 4 3 2 1)
```


> 这个例子接受了一个返回输入参数的倒数的键函数。得到的序列实际上是根据为所有项应用键函数得到的值进行排序的，而不是所有项本身的值。意思就是说，他们是根据自己的倒数来排序的。

> 三个参数的版本需要一个键函数，一个实现了`java.util.Comparator`的对象和一个序列/collection。它跟两个参数的版本只有一点差别，它使用这个比较器来进行排序。

  * split-at
> > `split-at`需要两个参数：一个数字和一个序列/collection。它返回一个拥有两个项的vector。第一个项是通过拿出原序列中前N个项组成的序列，N为第一个参数。第二个想是原序列中剩余的项。

```clj

user=> (split-at 2 [:a :b :c :d :e :f])
[(:a :b) (:c :d :e :f)]
```

  * split-with
> > `split-with`需要两个参数：一个单输入参数的断言函数和一个序列/collection。它返回一个拥有两个项的vector。第一个项是通过在原序列中从第一项开始应用断言函数，将直到第一个返回值为false的项之前的所有项拿出组成的新序列。第二项包含了原序列中剩下的所有项。

```clj

user=> (split-with pos? [2 1 5 -3 6 -2 -1])
[(2 1 5) (-3 6 -2 -1)]
```

  * partition
> > `partition`需要两个或三个输入参数。两个参数的版本需要一个数字和一个序列/collection，返回包含延迟加载序列的延迟加载序列（a lazy sequence of lazy sequences）。所有子序列都是由原序列中连续抽取出的N个项构成的，N是第一个输入参数。

```clj

user=> (partition 2 [:a :b :c :d :e :f])
((:a :b) (:c :d) (:e :f))
```


> 三个输入参数的版本需要两个数字和一个序列/collection。第一个数字和两个参数的版本痒痛，第二个数字为偏移值，即指构成每个子序列时在原序列中起始项前进时的步长。这样就使子序列间项的重叠成为可能。

```clj

user=> (partition 2 1 [:a :b :c :d :e :f])
((:a :b) (:b :c) (:c :d) (:d :e) (:e :f))
```

  * map
> > `map`需要一个函数作为第一个参数，然后是任意数量的序列/collection作为附加参数。提供的函数的输入参数个数应该与提供的附加序列的个数相当。它返回的延迟加载序列是通过在这些序列的每个项之上调用函数得到返回值来构建的。

```clj

user=> (map pos? [2 1 5 -3 6 -2 -1])
(true true true false true false false)

user=> (map + [2 4 8] [1 3 5])
(3 7 13)
```

  * first
> > `first`需要一个序列/collection作为参数。返回这个序列的第一个项，如果这个序列为空则返回nil。

```clj

user=> (first [1 2 3 4])
1
```

  * second
> > `second`需要一个序列/collection作为参数。返回这个序列的第二个项，如果这个序列为空则返回nil。

```clj

user=> (second [1 2 3 4])
2
```

  * nth
> > `nth`需要两个参数：一个序列/collection和一个数字。返回提供序列的第N项，N为第二个参数。序列是从0开始索引的，所以`(nth sequence 0)`等效与`(first sequence)`。如果这个序列少于N项，它会抛出一个error。

```clj

user=> (nth [:a :b :c :d] 2)
:c
```

  * last
> > `last`需要一个序列/collection作为参数。返回序列中最后一项。如果序列为空返回nil。

```clj

user=> (last [1 2 3 4])
4
```

  * reduce
> > `reduce`需要两个或三个输入参数。在两个参数的版本中，第一个参数是一个双输入参数的函数，第二个参数是一个序列/collection。`reduce`把这个函数应用在序列的头两个项之上，之后把这个函数应用在第一次调用的结果和之后的一个项之上，以此类推。

```clj

user=> (reduce + [1 2 3 4 5])
15
```


> 这个例子中，`reduce`把加法函数应用到一个整数的列表上，得到了这些整数的总和。

> 三个参数的版本比较相似，除了它需要一个函数，一个起始值和一个序列/collection。函数首先被应用到起始值和序列的第一个项之上，而不是序列的前两项。接下来的这个例子展示了从一个序列构建一个map的过程：使用每个项作为键，这个项的倒数作为值。一个空的map被作为初始值输入：

```clj

user=> (reduce (fn [my-map value]
(assoc my-map value (/ 1 value)))
{}
[1 2 3 4 5])
{5 1/5, 4 1/4, 3 1/3, 2 1/2, 1 1}
```

  * apply
> > `apply`需要两个或更多的参数。第一个参数是一个函数，最后一个参数是一个序列/collection。其他的参数可以使任意值。它返回将所有输入的值以及提供的序列中的所有值作为参数去调用这个函数。调用`(apply f a b [c d e])`与调用`(f a b c d e)`是完全相同的。`apply`的优势在于它可以构建动态的参数序列。

```clj

user=> (apply + 1 [2 3])
6
```


> 一个将`apply`应用到动态的参数列表上的例子：把0到5的整数作为参数调用`+`。这个调用与`(+ 1 2 3 4 5)`是完全相同的，除了参数列表是动态构建的。

```clj

user=> (apply + (range 1 6))
15
```

  * empty?
> > `empty?`使用蛋哥序列/collection作为参数。如果这个序列为空，则它返回true，反之则为false。

```clj

user=> (empty? [1 2 3 4])
false

user=> (empty? [])
true
```

  * some
> > `some`需要两个输入参数：一个单输入参数的断言函数和一个序列/collection。如果序列中只要有一个项不会使断言函数返回false或nil，它返回这个断言函数的返回值，否则返回nil。

```clj

user=> (some (fn [n] (< n 5)) [6 9 7 3])
true

user=> (some (fn [n] (< n 5)) [6 9 7 5])
nil
```

  * every?
> > `every?`需要两个输入参数：一个单输入参数的断言函数和一个序列/collection。如果对于序列中的所有项断言函数都返回true，则返回true，反之返回false。

```clj

user=> (every? (fn [n] (< n 5)) [2 1 4 3])
true

user=> (every? (fn [n] (< n 5)) [2 1 5 3])
false
```

  * dorun
> > `dorun`需要一个或两个输入参数：一个延迟加载的序列或者一个可选的数字和一个延迟加载的序列。它将导致这个延迟加载的序列被实现，仅仅是为了这个过程中的副作用。`dorun`总是返回nil而且不会保留列表的头，所以它不会消耗内存。


> 为了说明，接下来的这个例子将`map`函数应用在一个延迟加载的序列上，同时将`println`函数作为`map`的第二个参数。一般来讲，`println`不是作为`map`输入参数的好选择，因为它的执行只会带来副作用，同时返回nil。

```clj

user=> (def result (map println (range 1 5)))
#'user/result
user=> (dorun result)
1
2
3
4
nil
```

> 在这个例子中，`result`符号被绑定到一个延迟加载的序列上，即一个`map`函数的返回值。这个序列的所有实际值（所有项的值）都是nil，因为它们都是调用`println`的返回值。但这些值并没有在例子中使用到。但是，当序列的生成器函数（generator function）`println`被调用的时候，它会产生副作用。因为`map`返回的序列是延迟加载的，知道调用了`dorun`生成器函数才被触发，并强制序列被依次求值。
> 如果一个数字形式的参数被传递给了`dorun`，它只会对指定索引之前的项进行求值。

```clj

user=> (def result (map println (range 1 10)))
#’user/result
user=> (dorun 2 result)
1
2
3
nil
```

> 在用`dorun`调用一个无限长序列的时候需要非常小心，一定要保证提供的参数是数字，否则它的执行可能永远不会停止。

  * doall
> > `doall`与`dorun`几乎相同，除了一点，它会将序列求值的结果保存下来并将其返回。从本质上讲，`doall`会返回一个延迟加载序列的非延迟加载版本。这样做，它会导致与序列尺寸相当的内存消耗。在一个无限长序列上调用`doall`却不指定一个数字参数会倒是一个`OutOfMemoryError`，因为系统试着去缓存一个无限长的序列。

```clj

user=> (def result (map println (range 1 5)))
#’user/result
user=> (doall result)
1
2
3
4
(nil nil nil nil)
```


> 可以注意到，在为了得到副作用执行生成器函数之后，它还返回了调用`map`得到到的序列。在这里，他们全都是nil，调用`println`的返回值。

# 总结 #

使用序列的次数越多，你越能体会到序列的重要性。拥有一个高度整合的，极其强大并泛化collection管理库在手中，当你回到一个没有它的语言环境中，你会发现你很难去完成某个目标。

在编写符合Clojure语言习惯的代码时，你总在不停地使用序列。代码中任何可能会出现多个对象的地方都是一个可以使用序列来管理这个collection的潜在机会。这样做能免费地获得所有之前提到的这些序列函数，不管是内置的还是用户编写的。它们在编写富于表达，同时也十分简洁的程序时会带来巨大的助益。